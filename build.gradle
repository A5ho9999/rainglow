plugins {
    id "io.github.p03w.machete" version "1.2.0"
    id "fabric-loom" version "1.0-SNAPSHOT"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "org.quiltmc.quilt-mappings-on-loom" version "4.2.0"
    id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    maven { url = "https://maven.gegy.dev/" }
    maven { url = "https://maven.terraformersmc.com/releases/" }
}

configurations {
    shadow
    api.extendsFrom shadow
}

dependencies {
    // versions defined in gradle.properties
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings(loom.layered {
        addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${project.minecraft_version}+build.${project.quilt_mappings}:v2"))
    })
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    include(modImplementation("dev.lambdaurora:spruceui:${project.spruceui_version}"))
    modImplementation("com.terraformersmc:modmenu:${project.mod_menu_version}")
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

loom {
    accessWidenerPath = file("src/main/resources/rainglow.accesswidener")
}

modrinth {
    syncBodyFrom = rootProject.file("README.md").text
    changelog = rootProject.file("CHANGELOG.md").text
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "rainglow"
    versionNumber = project.version
    versionName = "rainglow " + project.version.toString().split("\\+")[0] + " for mc " + project.version.toString().split("\\+mc")[1]
    versionType = "beta"
    uploadFile = remapJar
    gameVersions = ["1.19", "1.19.1", "1.19.2"]
    loaders = ["quilt", "fabric"]
    dependencies {
        optional.project "modmenu"
    }

    // clear changelog
    rootProject.file("CHANGELOG.md").setText("")

    // sync readme
    tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
}

def targetJavaVersion = Integer.parseInt(project.java_version)
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = targetJavaVersion
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}
