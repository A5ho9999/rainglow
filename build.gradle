plugins {
    id "fabric-loom" version "1.7.+"
    id "com.github.johnrengelman.shadow" version "7.1.+"
    id "me.modmuss50.mod-publish-plugin" version "0.5.2"
}

version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    maven { url = "https://maven.gegy.dev/" }
    maven { url = "https://maven.terraformersmc.com/releases/" }
    maven { url = "https://maven.quiltmc.org/repository/release/" }
    maven { url = "https://repo.sleeping.town/" }
}

configurations {
    shadow
    api.extendsFrom shadow
}

dependencies {
    // versions defined in gradle.properties
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings("org.quiltmc:quilt-mappings:${project.minecraft_version}+build.${project.quilt_mappings}:intermediary-v2")
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

    modImplementation("com.terraformersmc:modmenu:${project.mod_menu_version}")
    implementation("folk.sisby:kaleido-config:${project.kaleido_config_version}")
    include("folk.sisby:kaleido-config:${project.kaleido_config_version}")

    Set<String> apiModules = [
            "fabric-networking-api-v1",
            "fabric-resource-loader-v0",
            "fabric-screen-api-v1",
            "fabric-key-binding-api-v1",
            "fabric-lifecycle-events-v1",
    ]

    apiModules.forEach {
        include(modImplementation(fabricApi.module(it, project.fabric_api_version)))
    }
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

loom {
    accessWidenerPath = file("src/main/resources/rainglow.accesswidener")
}

publishMods {
    displayName = "rainglow ${project.version}"
    file = remapJar.archiveFile
    changelog = rootProject.file("CHANGELOG.md").getText()
    type = STABLE

    modLoaders.add("quilt")
    modLoaders.add("fabric")

    dryRun = !providers.environmentVariable("MODRINTH_TOKEN").isPresent()
            || !providers.environmentVariable("CURSEFORGE_TOKEN").isPresent()
            || property("pub.should_publish") == "false"
    dryRun = false

    var mcVersions = [project.minecraft_version]
    mcVersions.addAll(property("pub.additional_versions").toString().split(" ").findAll { !it.empty })

    modrinth {
        projectId = "Bk6pUD7R"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        mcVersions.forEach(minecraftVersions::add)

        requires "fabric-api"
        optional "modmenu"
    }

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "682310"
        mcVersions.forEach(minecraftVersions::add)

        clientRequired = true
        serverRequired = false

        requires("fabric-api")
        optional("modmenu")
    }
}

def targetJavaVersion = Integer.parseInt(project.java_version)
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = targetJavaVersion
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}
